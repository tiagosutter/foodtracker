plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs'
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'br.dev.tiagosutter.foodtracker'
    compileSdk 33

    defaultConfig {
        applicationId "br.dev.tiagosutter.foodtracker"
        minSdk 22
        targetSdk 33
        versionCode 5
        versionName "1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    signingConfigs {
        releaseSignConfig {
            storeFile file(System.getenv("FOODTRACKER_KEYSTORE_FILE") ?: " ")
            storePassword System.getenv("FOODTRACKER_KEYSTORE_PWD")
            keyAlias System.getenv("FOODTRACKER_KEY_ALIAS")
            keyPassword System.getenv("FOODTRACKER_KEY_PWD")
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSignConfig


            firebaseAppDistribution {
                serviceCredentialsFile=System.getenv("FOODTRACKER_FIREBASE_SERVICE_CREDENTIALS_FILE" ?: " ")
                appId = "1:80023153180:android:e877ad6cc5df0738b6cdd9"
                releaseNotesFile = "app/releaseNotes.txt"
                groups = "developers, qa-team"
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:31.5.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    def room_version = "2.5.1"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    implementation 'com.github.bumptech.glide:glide:4.15.1'

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

}

// Allow references to generated code
kapt {
    correctErrorTypes true
}